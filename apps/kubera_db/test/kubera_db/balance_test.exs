defmodule KuberaDB.BalanceTest do
  use ExUnit.Case
  import KuberaDB.Factory
  alias KuberaDB.{Repo, Balance}
  alias Ecto.Adapters.SQL.Sandbox

  setup do
    :ok = Sandbox.checkout(Repo)
  end

  describe "factory" do
    test "has a valid factory" do
      {res, _balance} = Balance.insert(params_for(:balance))
      assert res == :ok
    end
  end

  describe "insert/1" do
    test "generates a UUID in place of a regular integer ID" do
      {res, balance} = :balance |> params_for |> Balance.insert

      assert res == :ok
      assert String.match?(balance.id,
        ~r/[0-9a-f]{8}-([0-9a-f]{4}-){3}[0-9a-f]{12}/)
    end

    test "generates an address as UUID if the address is not given" do
      {res, balance} =
        :balance |> params_for(%{address: nil}) |> Balance.insert

      assert res == :ok
      assert String.match?(balance.address,
        ~r/[0-9a-f]{8}-([0-9a-f]{4}-){3}[0-9a-f]{12}/)
    end

    test "generates the inserted_at and updated_at values" do
      {res, balance} = :balance |> params_for |> Balance.insert

      assert res == :ok
      assert balance.inserted_at != nil
      assert balance.updated_at != nil
    end

    test "allows insert if provided a user without minted_token" do
      {res, _balance} =
        :balance
        |> params_for(%{user: insert(:user), minted_token: nil})
        |> Balance.insert

      assert res == :ok
    end

    test "allows insert if provided a minted_token without user" do
      {res, _balance} =
        :balance
        |> params_for(%{minted_token: insert(:minted_token), user: nil})
        |> Balance.insert

      assert res == :ok
    end

    test "insert with autogenerated address if not given" do
      {res, balance} =
        :balance
        |> params_for(%{address: nil})
        |> Balance.insert

      assert res == :ok
      assert String.length(balance.address) > 0
    end

    test "insert with the specified address if given" do
      {res, balance} =
        :balance
        |> params_for(%{address: "the_address"})
        |> Balance.insert

      assert res == :ok
      assert balance.address == "the_address"
    end

    test "prevents creation of a balance with a blank address" do
      {result, changeset} =
        :balance
        |> params_for(%{address: ""})
        |> Balance.insert

      assert result == :error
      assert changeset.errors ==
        [address: {"can't be blank", [validation: :required]}]
    end

    test "prevents creation of a balance without a user/minted token/genesis" do
      {result, changeset} =
        :balance
        |> params_for(%{user: nil, minted_token: nil})
        |> Balance.insert

      assert result == :error
      assert changeset.errors ==
        [{[:account_id, :minted_token_id, :user_id, :genesis], {"can't all be blank", []}}]
    end

    test "prevents creation of a balance with both a user and minted token" do
      params = %{user: insert(:user), minted_token: insert(:minted_token)}
      {result, changeset} = :balance |> params_for(params) |> Balance.insert

      assert result == :error
      assert changeset.errors ==
        [{[:account_id, :minted_token_id, :user_id, :genesis],
         {"only one must be present", []}}]
    end

    test "returns error if balance with same address already exists" do
      {_result, _balance} =
        :balance
        |> params_for(%{address: "same_address"})
        |> Balance.insert

      {result, changeset} =
        :balance
        |> params_for(%{address: "same_address"})
        |> Balance.insert

      assert result == :error
      assert changeset.errors == [address: {"has already been taken", []}]
    end
  end

  describe "get/1" do
    test "returns an existing balance using an address" do
      :balance
      |> params_for(%{address: "balance_address1234"})
      |> Balance.insert

      balance = Balance.get("balance_address1234")
      assert balance.address == "balance_address1234"
    end

    test "returns nil if the balance address does not exist" do
      assert Balance.get("nonexisting_address") == nil
    end
  end

  describe "genesis/0" do
    test "inserts the genesis address if not existing" do
      assert Balance.get("genesis") == nil
      {:ok, genesis} = Balance.genesis()
      assert Balance.get("genesis") == genesis
    end

    test "returns the existing genesis address if present" do
      {:ok, inserted_genesis} = Balance.genesis()
      {:ok, genesis} = Balance.genesis()
      assert inserted_genesis == genesis
    end
  end
end
